%union {
  long l;
  char *s;
  double f;
}

%token <s> STRING
%token NODE
%token LAT
%token LON
%token <l> INTEGER
%token <f> FLOAT
%token <s>COUNTRYCODE
%token <s>COUNTRY
%token <s>CITY
%token <s>STREET
%token <s>NUMBER
%token <s>POSTCODE

%type<l> num
%type<f> coor

%{

using namespace std;

#include <string>
#include <stdexcept>
#include "routeinfo.h"

int yylex(void);
void yyerror (char *s) {
     throw runtime_error(s);
}

void yyerror (string s) {
     throw runtime_error(s);
}

int locptr;

%}

%%

route		: vehicle '(' start ',' stop ')'
		;

vehicle		: STRING {inforoute.vehicle = $1;}
		;

start		: {locptr=0;} locatie
		;

stop		: {locptr=1;} locatie
		;

locatie		: nodeloc {inforoute.locations[locptr].node=true;}
		| coorloc {inforoute.locations[locptr].coord=true;}
		| addrloc {inforoute.locations[locptr].address=true;}
		;

nodeloc		: NODE '=' num {inforoute.locations[locptr].nodeid=$3;}
		| num {inforoute.locations[locptr].nodeid=$1;}
		;

coorloc		: LAT '=' coor ',' LON '=' coor {inforoute.locations[locptr].lat=$3;inforoute.locations[locptr].lon=$7;}
		| LON '=' coor ',' LAT '=' coor {inforoute.locations[locptr].lat=$7;inforoute.locations[locptr].lon=$3;}
		| '(' coor ',' coor ')' {inforoute.locations[locptr].lat=$2;inforoute.locations[locptr].lon=$4;}
		;

num		: INTEGER
		;

coor		: INTEGER {$$=(double)$1;}
		| FLOAT
		;


addrloc		: addr_elem
		| addrloc addr_elem
		;

addr_elem	: country
		| city
		| street
		| number
		| postcode
		; 

country		: COUNTRY '=' COUNTRYCODE {inforoute.locations[locptr].country=$3;}
		;

city		: CITY '=' STRING {inforoute.locations[locptr].city=$3;}
		;

street		: STREET '=' STRING {inforoute.locations[locptr].street=$3;}
		;
	
number		: NUMBER '=' STRING {inforoute.locations[locptr].number=$3;}
		;

postcode	: POSTCODE '=' STRING {inforoute.locations[locptr].postcode=$3;}
		;
%%
